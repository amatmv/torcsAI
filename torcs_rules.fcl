//---------------------------------------------------------------

FUNCTION_BLOCK acceleracio

VAR_INPUT
        curva : REAL;
        velocitat : REAL;
END_VAR

VAR_OUTPUT
        acceleracio : REAL;
        fre : REAL;
END_VAR

FUZZIFY curva
        TERM esquerre := trape 0.5 0.6 1.0 1.0;
        TERM centre := trape -0.5 -0.0 0.0 0.5;
        TERM dreta := trape -1.0 -1.0 -0.6 -0.5;
END_FUZZIFY

FUZZIFY velocitat
        TERM lent := trape 0.0 1.0 50.0 70.0;
        TERM mig := trape 70.0 90.0 110.0 120.0;
        TERM toreto := trape 110.0 120.0 300.0 300.0;
END_FUZZIFY

DEFUZZIFY acceleracio
        TERM no := trian 0.0 0.0 0.0;
        TERM mig := trape 0.1 0.2 0.4 0.5;
        TERM gas := trape 0.5 0.6 0.8 0.9;
        METHOD : COG;
        DEFAULT := 0.8;
END_DEFUZZIFY

DEFUZZIFY fre
        TERM freDeMa := trape 0.1 0.1 1.0 1.0;
        METHOD : COG;
        DEFAULT := 0.0;
END_DEFUZZIFY

RULEBLOCK acceleracio

        AND : MIN;
        ACT : MIN;
        ACCU : MAX;

        RULE 1 : IF velocitat IS toreto THEN acceleracio IS mig;

        RULE 2 : IF (curva IS dreta OR curva IS esquerre) AND velocitat IS toreto THEN fre IS freDeMa;
        RULE 3 : IF curva IS dreta OR curva IS esquerre THEN acceleracio IS mig;


END_RULEBLOCK

END_FUNCTION_BLOCK


//---------------------------------------------------------------

FUNCTION_BLOCK gir

VAR_INPUT
        acceleracio : REAL;
        fre : REAL;
        distVorals : REAL;
        angleCentre : REAL;
        curva : REAL;
END_VAR

VAR_OUTPUT
        acceleracioOut : REAL;
        gir : REAL;
END_VAR

FUZZIFY angleCentre
        TERM esquerre := trape -3.150 -3.150 -0.900 -0.000;
        TERM centre := trape -0.900 -0.000 0.000 0.900;
        TERM dret := trape 0.000 0.900 3.150 3.150;
END_FUZZIFY

FUZZIFY acceleracio
        TERM no := 0.0;
        TERM mig := trape 0.1 0.2 0.6 0.7;
        TERM gas := trape 0.7 0.7 0.8 0.9;
END_FUZZIFY

FUZZIFY distVorals
        TERM dret := trape -0.9 -0.9 -0.7 -0.6;
        TERM mig := trian -0.6 -0.0 0.6;
        TERM esquerre := trape 0.6 0.7 0.9 0.9;
END_FUZZIFY

FUZZIFY fre
        TERM no := (0.0, 0);
        TERM freDeMa := (0.1, 0) (0.5, 1) (1.0, 1);
END_FUZZIFY

FUZZIFY curva
        TERM esquerre := trape 0.5 0.6 1.0 1.0;
        TERM centre := trape -0.5 -0.0 0.0 0.5;
        TERM dreta := trape -1.0 -1.0 -0.6 -0.5;
END_FUZZIFY

DEFUZZIFY acceleracioOut
        TERM no := trian 0.0 0.1 0.1;
        TERM mig := trape 0.1 0.2 0.6 0.7;
        TERM gas := trape 0.7 0.7 0.8 0.9;
        METHOD : COG;
        DEFAULT := 0.0;
END_DEFUZZIFY

DEFUZZIFY gir
        TERM esquerre := trape 0.000 0.100 1.000 1.000;
        TERM no := trian -0.100 0.000 0.100;
        TERM dret := trape -1.000 -1.000 -0.100 0.000;

        METHOD : COG;
        DEFAULT := 0.0;
END_DEFUZZIFY

RULEBLOCK gir

        AND : MIN;
        ACT : MIN;
        ACCU : MAX;

        RULE 1 : IF distVorals IS esquerre THEN gir is dret;
        RULE 2 : IF distVorals IS dret THEN gir is esquerre;
        RULE 3 : IF angleCentre IS esquerre THEN gir is dret;
        RULE 4 : IF angleCentre IS dret THEN gir is esquerre;

        RULE 5 : IF distVorals IS mig THEN gir IS no;

        RULE 6 : IF curva IS centre THEN gir is no;
        RULE 10 : IF curva is esquerre THEN gir is esquerre;
        RULE 20 : IF curva is dreta THEN gir is dret;

END_RULEBLOCK

END_FUNCTION_BLOCK


//---------------------------------------------------------------


FUNCTION_BLOCK outgear

VAR_INPUT
        acceleracio : REAL;
        rpm : REAL;
END_VAR

VAR_OUTPUT
        outgear : REAL;
END_VAR

FUZZIFY acceleracio
        TERM no := (0.0,1)(0.1,0);
        TERM mig := (0.1,0)(0.2,1)(0.6,1)(0.7,0);
        TERM gas := (0.7,0)(0.7,1)(0.8,1)(0.9,0);
END_FUZZIFY

FUZZIFY rpm
        TERM baixa := (0.0, 1)(2500.0, 1)(3000.0, 0);
        TERM bona := (3000.0, 0)(3000,1)(4500,1)(7500.0,0);
        TERM alta := (7500.0,0)(8500.0,1)(12000.0,1);
END_FUZZIFY

DEFUZZIFY outgear
        TERM up := 1.0;
        TERM same := 0.0;
        TERM down := -1.0;
        METHOD : COGS;
        DEFAULT := 0.0;
END_DEFUZZIFY


RULEBLOCK shift

        AND : MIN;
        ACT : MIN;
        ACCU : MAX;

        RULE 1 : IF rpm IS alta AND acceleracio IS NOT no THEN outgear IS up;
        RULE 2 : IF rpm IS bona THEN outgear IS same;
        RULE 3 : IF rpm IS baixa AND acceleracio IS NOT gas THEN outgear IS down;

END_RULEBLOCK

END_FUNCTION_BLOCK

//---------------------------------------------------------------
